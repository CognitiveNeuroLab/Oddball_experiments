# Oddball_discriminate_duration 115ms dev
# standard p(.85)/deviant p(0.15)
# first 4 of each block are standards
# deviant always follows at least two standards
# SOA = 900 for STD & DEV
# ISI = 800 for STD but 785 for DEV

string direc = scenario_directory;
#

# Set parameters:
int ntrls = 240; # number of trials (this is the number in the sequence text files)

# array to be filled from pre-made sequence text files
array<int> trl_order[ntrls]; 

# Begin:

# Read-in the trial order
input_file order_in = new input_file;
string file_path = direc + "sequences\\" + printf(random(1,50),"115_sequence_%d.txt");
#string file_path = direc + "sequences\\" + printf(1,"115_sequence_%d.txt");
order_in.open(file_path);
order_in.set_delimiter( '\n' ); # for get_line()
		
int trl = 1;
loop until
	trl > ntrls
begin
	trl_order[trl] = order_in.get_int();
	trl = trl+1;
end;

order_in.close();
#starting with trigger 201 so biosemi confg file starts saving automatic
start_saving.present();
# add a pause at the beginning of each block
nothing_trial.present();

# run 4 standard trials at the begining before presenting any deviants	
loop
	int i = 1
until
	i > 4
begin
	
	isi_trial.set_duration(800);
	standard_trial.present();
	isi_trial.present();
	i = i + 1;
	
end;
	
loop
	int t = 1
until
	t > ntrls
begin
	
	if trl_order[t] == 2 then # present the deviant 
		isi_trial.set_duration(785);
		deviant_trial.present();
	else
		isi_trial.set_duration(800);
		standard_trial.present();
	end;
	
	isi_trial.present();
	
	t = t + 1;
		
end;
	
end_block_trial.present();
	



